allprojects {
    group 'dev.xdark.asm-inline'
    version '1.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    configurations {
        shade
        compileOnly.extendsFrom shade
    }

    java.toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    project.ext {
        asm = 'org.ow2.asm:asm:9.6'
    }

    publishing {
        repositories {
            if (System.getenv('CI') == null) {
                mavenLocal()
            } else {
                maven {
                    url = 'https://repo.implario.dev/public/'
                    credentials {
                        username System.getenv('IMPLARIO_REPO_USER')
                        password System.getenv('IMPLARIO_REPO_PASSWORD')
                    }
                }
            }
        }
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}
